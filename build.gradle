plugins {
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version '5.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

repositories {
	maven {
		name "AnonymousHacker1279"
		url "https://maven.anonymoushacker1279.tech/releases"
		credentials(PasswordCredentials)
		authentication {
			basic(BasicAuthentication)
		}
	}
	maven {
		name "Jared's Maven"
		url 'https://maven.blamejared.com'
	}
	maven {
		name "ModMaven"
		url "https://modmaven.dev/"
	}
	maven {
		name "Maven Central"
		url "https://repo.maven.apache.org/maven2"
	}
	maven {
		name "Bai Maven"
		url "https://maven.bai.lol"
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name "Modding Legacy Maven"
		url "https://maven.moddinglegacy.com/artifactory/modding-legacy/"
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
	mappings channel: mappings_channel, version: mappings_version

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For dependencies to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			jvmArgs '-Xmx6G', '-XX:+UseShenandoahGC'

			mods {
				iwcompatbridge {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For dependencies to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			jvmArgs '-Xmx6G', '-XX:+UseShenandoahGC'

			mods {
				iwcompatbridge {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For dependencies to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			args '--mod', 'iwcompatbridge', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			jvmArgs '-Xmx6G', '-XX:+UseShenandoahGC'

			mods {
				iwcompatbridge {
					source sourceSets.main
				}
			}
		}
	}
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	implementation 'org.jetbrains:annotations:23.0.0'

	minecraft "net.minecraftforge:forge:${forge_version}"

	// Include Immersive Weapons
	implementation fg.deobf("tech.anonymoushacker1279.immersiveweapons:ImmersiveWeapons:${immersiveweapons_version}")
	// Include TerraBlender (for IW)
	implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${terrablender_version}")

	// Include JEI
	compileOnly fg.deobf("mezz.jei:jei-1.19.3-common-api:${jei_version}")
	compileOnly fg.deobf("mezz.jei:jei-1.19.3-forge-api:${jei_version}")
	runtimeOnly fg.deobf("mezz.jei:jei-1.19.3-forge:${jei_version}")
	// Include WTHIT and badpackets
	compileOnly fg.deobf("mcp.mobius.waila:wthit-api:forge-${wthit_version}")
	runtimeOnly fg.deobf("mcp.mobius.waila:wthit:forge-${wthit_version}")
	implementation fg.deobf("lol.bai:badpackets:forge-${badpackets_version}")
	// Include Project MMO
	// TODO: Uncomment when released for 1.19.3
	//implementation fg.deobf("curse.maven:projectmmo-353935:${project_mmo_version}")
	// Include Lucent
	implementation fg.deobf("curse.maven:lucent-493280:${lucent_version}")
}

// Add properties to JAR manifest
jar {
	manifest {
		attributes([
				"Specification-Title"     : "iwcompatbridge",
				"Specification-Vendor"    : "anonymoushacker1279",
				"Specification-Version"   : "1", // We are version 1 of ourselves
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : "${version}",
				"Implementation-Vendor"   : "anonymoushacker1279",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		repositories {
			maven {
				name "AnonymousHacker1279"

				def baseMavenUrl = "https://maven.anonymoushacker1279.tech/"
				url version.contains("SNAPSHOT") ? baseMavenUrl + "snapshots" : baseMavenUrl + "releases"

				credentials(PasswordCredentials)
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
	options.fork = true // Use a separate JVM process for Java compilation
}