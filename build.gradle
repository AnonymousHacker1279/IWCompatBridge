plugins {
	id 'java-library'
	id 'maven-publish'
	// Pinned because it updates too frequently and forces recompile
	id 'net.neoforged.gradle.userdev' version '7.0.83'
	id "com.hypherionmc.modutils.modpublisher" version "2.0.0"
	id 'idea'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

import java.security.MessageDigest

repositories {
	mavenLocal()
	maven {
		name "AnonymousHacker1279"

		def baseMavenUrl = "https://maven.anonymoushacker1279.tech/"
		url immersiveweapons_version.contains("SNAPSHOT") ? baseMavenUrl + "snapshots" : baseMavenUrl + "releases"

		credentials(PasswordCredentials)
		authentication {
			basic(BasicAuthentication)
		}
	}
	maven {
		name "Jared's Maven"
		url 'https://maven.blamejared.com'
	}
	maven {
		name "ModMaven"
		url "https://modmaven.dev/"
	}
	maven {
		name "Maven Central"
		url "https://repo.maven.apache.org/maven2"
	}
	maven {
		name "Bai Maven"
		url "https://maven2.bai.lol"
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name "Modding Legacy Maven"
		url "https://maven.moddinglegacy.com/artifactory/modding-legacy/"
	}
	maven {
		name "TheIllusiveC4 Maven"
		url = "https://maven.theillusivec4.top/"
	}
}


version = mod_version
group = mod_group_id

base {
	archivesName = project.mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
	main {
		resources {
			// Include resources generated by data generators.
			srcDirs += "src/generated/resources"
		}
	}
	generated

	// Separate datagen sourceSet from main to shrink the build jar
	datagen {
		compileClasspath += main.output
	}
}

// Ensure other source sets receive the necessary dependencies
configurations {
	extendConfigurations(implementation, testImplementation, datagenImplementation)
	extendConfigurations(compileOnly, testCompileOnly, datagenCompileOnly)
	extendConfigurations(runtimeOnly, testRuntimeOnly, datagenRuntimeOnly)
}

static void extendConfigurations(Configuration base, Configuration... configurations) {
	for (configuration in configurations) {
		configuration.extendsFrom(base)
	}
}

runs {
	configureEach {
		systemProperty 'forge.logging.markers', 'REGISTRIES'
		systemProperty 'forge.logging.console.level', 'debug'

		jvmArguments.addAll '-Xmx6G'

		// If the selected toolchain is a JBR, enable DCEVM for improved hotswapping
		if (it.project.javaToolchains.launcherFor(java.toolchain).map { it.metadata.vendor }.getOrElse("").contains("JetBrains")) {
			jvmArguments.addAll '-XX:+AllowEnhancedClassRedefinition'
		}

		modSources project.sourceSets.main, project.sourceSets.datagen
	}

	client {
		// Setup client information from gradle.properties in the user home
		// All of these are optional
		if (project.hasProperty('mc_uuid')) {
			programArguments('--uuid', (String) project.property('mc_uuid'))
		}
		if (project.hasProperty('mc_username')) {
			programArguments('--username', (String) project.property('mc_username'))
		}
		if (project.hasProperty('mc_accessToken')) {
			programArguments('--accessToken', (String) project.property('mc_accessToken'))
		}
	}

	server {
		programArgument '--nogui'
	}

	data {
		programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

// Include resources generated by data generators
sourceSets.main.resources {
	srcDir 'src/generated/resources'
	// Exclude the cache directory
	exclude '.cache'
}

dependencies {
	implementation 'org.jetbrains:annotations:24.0.1'

	implementation "net.neoforged:neoforge:${neoforge_version}"

	// Include Immersive Weapons
	implementation "tech.anonymoushacker1279.immersiveweapons:ImmersiveWeapons:${immersiveweapons_version}"

	// Include JEI
	compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
	compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
	runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
	// Include WTHIT and badpackets
	compileOnly "mcp.mobius.waila:wthit-api:neo-${wthit_version}"
	runtimeOnly "mcp.mobius.waila:wthit:neo-${wthit_version}"
	compileOnly "lol.bai:badpackets:neo-${badpackets_version}"
	// Include Project MMO
	// implementation fg.deobf("curse.maven:projectmmo-353935:${project_mmo_version}")
	// Include Lucent
	implementation "com.legacy:lucent:${lucent_version}"
	// Include Curios
	runtimeOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}"
	compileOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}:api"
	// Include Mekanism
	// implementation fg.deobf("mekanism:Mekanism:${mekanism_version}")
	// runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")
}

tasks.named('processResources', ProcessResources).configure {
	def replaceProperties = [
			minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
			neoforge_version    : neoforge_version, neoforge_version_range: neoforge_version_range,
			loader_version_range: loader_version_range, mod_version: mod_version
	]
	inputs.properties replaceProperties

	filesMatching(['META-INF/mods.toml']) {
		expand replaceProperties + [project: project]
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
	// Minify JSON files
	doLast {
		def minifyStart = System.currentTimeMillis()
		def filesMinified = 0
		def bytesSaved = 0

		fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
			File file = it
			filesMinified++
			def oldLength = file.length()
			file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
			bytesSaved += (oldLength - file.length())
		}

		// Format the bytes saved to KB, and add commas
		bytesSaved = String.format("%,d", (bytesSaved / 1024).toLong())

		println('Minified ' + filesMinified + ' JSON files, saving ' + bytesSaved + ' KB. Took ' + (System.currentTimeMillis() - minifyStart) + 'ms.')
	}
}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
				'Specification-Title'     : mod_id,
				'Specification-Vendor'    : "AnonymousHacker1279",
				'Specification-Version'   : '1',
				'Implementation-Title'    : project.name,
				'Implementation-Version'  : project.jar.archiveVersion,
				'Implementation-Vendor'   : "AnonymousHacker1279",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		register('mavenJava', MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			name "AnonymousHacker1279"

			def baseMavenUrl = "https://maven.anonymoushacker1279.tech/"
			url mod_version.contains("SNAPSHOT") ? baseMavenUrl + "snapshots" : baseMavenUrl + "releases"

			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

if (file("RELEASE_NOTES.md").exists()) {
	publisher {
		apiKeys {
			// Load API keys from tokens.properties file
			def tokens = new Properties()
			file("tokens.properties").withInputStream {
				stream -> tokens.load(stream)
			}

			curseforge = tokens.getProperty("curseforge")
			modrinth = tokens.getProperty("modrinth")
		}

		setDebug(false)
		curseID = "633345"
		modrinthID = "SAUGybfH"
		githubRepo = "AnonymousHacker1279/IWCompatibilityBridge"
		versionType = "release"
		// Read the contents of the RELEASE_NOTES.md file
		changelog = file("RELEASE_NOTES.md").text
		// Get the IWCB version from the mod_version variable, it's the last part of the version string
		version = "v" + mod_version.split("-")[1]
		displayName = "Immersive Weapons Compatibility Bridge - " + version
		gameVersions = [minecraft_version]
		loaders = ["neoforge"]
		curseEnvironment = "both"
		artifact = jar

		curseDepends {
			required "immersive-weapons"
			optional "jei", "wthit-forge", "curios", "lucent", "project-mmo"
		}

		modrinthDepends {
			required "QZFdnUQ5"
			optional "u6dRKJwZ", "6AQIaxuO", "vvuO3ImH", "JE0fgMUp", "KFQYC1Uy"
		}
	}
} else {
	println("No RELEASE_NOTES.md found, publishing outside of Maven is disabled.")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
	options.fork = true // Use a separate JVM process for Java compilation
}

void minimizePNGFile(File file) {
	long size = file.length()
	exec {
		executable "optipng"
		args "-q", "-o7", "-zm1-9", "-strip", "all", file
	}
	long newSize = file.length()
	if (newSize < size) {
		System.out.format("Reduced file size of %s from %d bytes to %d bytes (reduced by %.2f%%)\n",
				file, size, newSize, ((double) (size - newSize)) / ((double) size) * 100.0)
	}
}

static def calculateMD5Hash(File file) {
	MessageDigest md = MessageDigest.getInstance("MD5")
	file.withInputStream { md.update(it.bytes) }
	byte[] digest = md.digest()
	return digest.collect { String.format("%02x", it) }.join()
}

tasks.register('optimizePng').configure {
	doLast {
		try {
			exec {
				commandLine "optipng", "-v"
			}
			// OptiPNG is available, continue with the task
			def pngPatterns = ["**/*.png"]

			// Load cache with file hashes, to avoid re-optimizing files that are untouched
			def cache = [:]
			if (file(".optimizePngCache").exists()) {
				cache = new JsonSlurper().parse(file(".optimizePngCache"))
				println("Loaded " + cache.size() + " entries from cache.")
			} else {
				file(".optimizePngCache").createNewFile()
			}

			// Minimize any PNGs found
			def originalSize = 0
			def newSize = 0
			def imagesMinimized = 0

			for (dir in sourceSets.main.resources.srcDirs) {
				fileTree(dir: dir, includes: pngPatterns).each {
					// Skip files that are already optimized
					if (cache.containsKey(it.path) && cache[it.path] == calculateMD5Hash(it)) {
						return
					} else {
						imagesMinimized++
						originalSize += it.length()
						minimizePNGFile(it)
						newSize += it.length()

						// Update the cache with the new file hash
						cache[it.path] = calculateMD5Hash(it)
					}
				}
			}

			if (imagesMinimized > 0) {
				// Format the bytes saved to KB, and add commas
				def bytesSaved = String.format("%,d", ((originalSize - newSize) / 1024).toLong())
				println("Minimized " + imagesMinimized + " PNGs, saving " + bytesSaved + " KB.")

				// Save the cache
				file(".optimizePngCache").text = JsonOutput.toJson(cache)
				println("Saved " + cache.size() + " entries to cache.")
			} else {
				println("No PNGs were optimized.")
			}
		} catch (Exception e) {
			// OptiPNG is not available
			println("An error occurred while attempting to optimize PNGs.")
			println("OptiPNG may not be available. Please consider installing OptiPNG, and ensure it is added to your PATH.")
			println("https://optipng.sourceforge.net/")
		}
	}
}